dnl
dnl Process this file with autoconf to produce a configure script.
dnl

AC_INIT(fingerd.c)
AC_PREREQ(2.12)dnl Required Autoconf version.

AC_REVISION([#ident "@(#)$Name:  $:$Id: configure.in,v 1.16 1999/01/17 01:49:36 woods Exp $"])dnl

PACKAGE=fingerd
VERSION=1.3.95-Preview

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

AM_SANITY_CHECK

AC_ARG_PROGRAM

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_LANG_C

AC_PROG_MAKE_SET

AM_PROG_INSTALL
AC_PROG_LN_S

missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)
AM_MISSING_PROG(MAKEINFO, makeinfo, $missing_dir)

AC_PATH_PROG(PATH_FINGER, finger, , $PATH:/usr/local/bin:/usr/ucb:/usr/bsd:/usr/new)
if test -z "$PATH_FINGER" ; then
	AC_MSG_ERROR(the finger program cannot be found)
fi
AC_DEFINE_UNQUOTED(PATH_FINGER, "$PATH_FINGER")

dnl PATH_FINGERD is not used except to set syslibexecdir
AC_PATH_PROG(PATH_FINGERD, fingerd, , /usr/etc:/etc:/usr/libexec)
if test -n "$PATH_FINGERD" ; then
	syslibexecdir=`dirname $PATH_FINGERD`
fi
dnl INPATH_FINGERD is not used except to set syslibexecdir
AC_PATH_PROG(INPATH_FINGERD, in.fingerd, , /usr/etc:/etc:/usr/libexec)
if test -n "$INPATH_FINGERD" ; then
	syslibexecdir=`dirname $INPATH_FINGERD`
fi
AC_MSG_CHECKING(path to system daemons)
if test -z "$syslibexecdir" ; then
	AC_MSG_WARN(cannot find original system fingerd)
	syslibexecdir=NO_EXEC_DIR	
else
	AC_MSG_RESULT($syslibexecdir)
fi
AC_SUBST(syslibexecdir)

dnl Try to get a POSIX.1 environment.
dnl we need to call these system-specific macros early...
AC_AIX
AC_MINIX
AC_ISC_POSIX

dnl Checks for libraries.
AC_CHECK_LIB(sun, getpwnam)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h unistd.h string.h syslog.h \
sys/types.h sys/socket.h netinet/in.h arpa/inet.h)

AC_MSG_CHECKING(for sys/cdefs.h)
AC_TRY_CPP([<sys/cdefs.h>], fd_cv_have_sys_cdefs_h=yes, fd_cv_have_sys_cdefs_h=no)
AC_MSG_RESULT($fd_cv_have_sys_cdefs_h)
if test $fd_cv_have_sys_cdefs_h = no ; then
	# ideally this would be '-I$(srcdir)/compat'
	CPPFLAGS="-Icompat $CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(strdup strerror strtok)

dnl XXX asprintf should be handled with AC_REPLACE_FUNCS but I do not
dnl have a good portable replacement for it just now....
AC_CHECK_FUNCS(asprintf inet_ntoa memcpy syslog socket strchr)
if test $ac_cv_func_inet_ntoa = no; then
	# inet_ntoa is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, inet_ntoa, break)
	done
fi
if test $ac_cv_func_inet_ntoa = no; then
	AC_MSG_ERROR(the inet_ntoa function cannot be found)
fi
if test $ac_cv_func_syslog = no; then
	# syslog is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, syslog, break)
	done
fi
dnl XXX We could possibly use fakelog.c from tcp_wrappers too, but if
dnl you get this far and there is really no syslog() then something isc
dnl likely wrong...
if test $ac_cv_func_syslog = no; then
	AC_MSG_ERROR(the syslog function cannot be found)
fi
if test $ac_cv_func_socket = no; then
	# socket is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, socket, break)
	done
fi
if test $ac_cv_func_socket = no; then
	AC_MSG_ERROR(the socket function cannot be found)
fi

dnl this is a horrible hack necessary because configure does not
dnl normally do this until just before it writes out config.status
case "$sysconfdir" in
/*)
	PATH_SYSCONFDIR=$sysconfdir
	;;
*)
	oldprefix=$prefix
	test "x$prefix" = xNONE && prefix=$ac_default_prefix
        PATH_SYSCONFDIR=`eval echo $sysconfdir`
        prefix=$oldprefix
        ;;
esac

AC_SUBST(PATH_SYSCONFDIR)
AC_DEFINE_UNQUOTED(PATH_SYSCONFDIR, "$PATH_SYSCONFDIR")

AC_OUTPUT([Makefile Makefile.BSD fingerd.8 samples/Makefile])
