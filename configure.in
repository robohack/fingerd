dnl
dnl Process this file with autoconf to produce a configure script.
dnl

AC_INIT(fingerd.c)
AC_PREREQ(2.12)dnl Required Autoconf version.

AC_REVISION([#ident "@(#)fingerd:$Name:  $:$Id: configure.in,v 1.4 1997/04/07 18:44:06 woods Exp $"])dnl

PACKAGE=fingerd
VERSION=1.3.80

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

AM_SANITY_CHECK

AC_ARG_PROGRAM

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_LANG_C

AC_PROG_MAKE_SET

AM_PROG_INSTALL
AC_PROG_LN_S

missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)
AM_MISSING_PROG(MAKEINFO, makeinfo, $missing_dir)

AC_PATH_PROG(FINGER_PATH, finger, , $PATH:/usr/local/bin:/usr/ucb:/usr/bsd:/usr/new)
if test -z "$FINGER_PATH" ; then
	AC_MSG_ERROR(the finger program cannot be found)
fi
AC_DEFINE_UNQUOTED(FINGER_PATH, "$FINGER_PATH")

dnl FINGERD_PATH isn't used except to set syslibexecdir
AC_PATH_PROG(FINGERD_PATH, fingerd, , /usr/etc:/etc:/usr/libexec)
if test -n "$FINGERD_PATH" ; then
	syslibexecdir=`dirname $FINGERD_PATH`
fi
dnl INFINGERD_PATH isn't used except to set syslibexecdir
AC_PATH_PROG(INFINGERD_PATH, in.fingerd, , /usr/etc:/etc:/usr/libexec)
if test -n "$INFINGERD_PATH" ; then
	syslibexecdir=`dirname $INFINGERD_PATH`
fi
AC_MSG_CHECKING(path to system daemons)
if test -z "$syslibexecdir" ; then
	AC_MSG_WARN(cannot find original system fingerd)
	syslibexecdir=NO_EXEC_DIR	
else
	AC_MSG_RESULT($syslibexecdir)
fi
AC_SUBST(syslibexecdir)

dnl Try to get a POSIX.1 environment.
dnl we need to call these system-specific macros early...
AC_AIX
AC_MINIX
AC_ISC_POSIX

dnl Checks for libraries.
AC_CHECK_LIB(sun, getpwnam)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h unistd.h string.h syslog.h \
sys/types.h sys/socket.h netinet/in.h arpa/inet.h)

AC_MSG_CHECKING(for sys/cdefs.h)
AC_TRY_CPP([<sys/cdefs.h>], fd_cv_have_sys_cdefs_h=yes, fd_cv_have_sys_cdefs_h=no)
AC_MSG_RESULT($fd_cv_have_sys_cdefs_h)
if test $fd_cv_have_sys_cdefs_h = no ; then
	# ideally this would be '-I$(srcdir)/compat'
	CPPFLAGS="-Icompat $CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VFORK
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(strdup strerror strtok)

AC_CHECK_FUNCS(inet_ntoa syslog socket strchr strdup)
if test $ac_cv_func_inet_ntoa = no; then
	# inet_ntoa is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, inet_ntoa, break)
	done
fi
if test $ac_cv_func_inet_ntoa = no; then
	AC_MSG_ERROR(the inet_ntoa function cannot be found)
fi
if test $ac_cv_func_syslog = no; then
	# syslog is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, syslog, break)
	done
fi
if test $ac_cv_func_syslog = no; then
	AC_MSG_ERROR(the syslog function cannot be found)
fi
if test $ac_cv_func_socket = no; then
	# socket is not in the default libraries.  See if it's in some other.
	for lib in bsd socket inet nsl; do
		AC_CHECK_LIB($lib, socket, break)
	done
fi
if test $ac_cv_func_socket = no; then
	AC_MSG_ERROR(the socket function cannot be found)
fi

dnl this is a horrible hack necessary because configure doesn't
dnl normally do this until just before it writes out config.status
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix=$prefix

FQSBINDIR=`eval echo $sbindir`
AC_DEFINE_UNQUOTED(FQSBINDIR, "$FQSBINDIR")
AC_SUBST(FQSBINDIR)

FINGER_MOTD=`eval echo $datadir/$PACKAGE/motd`
AC_DEFINE_UNQUOTED(FINGER_MOTD, "$FINGER_MOTD")
AC_SUBST(FINGER_MOTD)

FINGER_ACL=`eval echo $datadir/$PACKAGE/access`
AC_DEFINE_UNQUOTED(FINGER_ACL, "$FINGER_ACL")
AC_SUBST(FINGER_ACL)

FINGER_USERS=`eval echo $datadir/$PACKAGE/users`
AC_DEFINE_UNQUOTED(FINGER_USERS, "$FINGER_USERS")
AC_SUBST(FINGER_USERS)

AC_OUTPUT([Makefile fingerd.8 samples/Makefile])
